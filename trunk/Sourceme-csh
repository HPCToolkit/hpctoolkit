# -*-Mode: sh;-*-
# $Id$

#############################################################################

if ( ! $?HPCTOOLKIT ) then
  echo "You must set your HPCTOOLKIT environment variable before sourcing"
  echo "this file."
  exit 1
endif

#############################################################################
# Set platform variables
#############################################################################

pushd ${HPCTOOLKIT}/src/config >& /dev/null
setenv HPCTOOLKIT_PLATFORM `./hpcplatform`
popd >& /dev/null

#############################################################################
# Set other variables
#############################################################################

set HPCPERLLIB="${HPCTOOLKIT}/bin/perl"

if ( ! $?PERLLIB ) then
  setenv PERLLIB "${HPCPERLLIB}"
else
  setenv PERLLIB "${HPCPERLLIB}:${PERLLIB}"
endif

if ( ! $?PERL5LIB ) then
  setenv PERL5LIB "${HPCPERLLIB}"
else
  setenv PERL5LIB "${HPCPERLLIB}:${PERL5LIB}"
endif

#############################################################################

# When HPCToolkit is formally installed, xerces will be in a different
# location

set installedXerces="${HPCTOOLKIT}/lib/xercesc"

if ( -d "${installedXerces}" ) then
  setenv XERCESCROOT "${installedXerces}"
else
  setenv XERCESCROOT "${HPCTOOLKIT}/../xercesc"
endif

set XERCESPLATFORM="${XERCESCROOT}/${HPCTOOLKIT_PLATFORM}/lib"


if ( ${HPCTOOLKIT_PLATFORM} == 'mips-IRIX64' ) then
  # set library path for IRIX systems
  if ( ! $?LD_LIBRARYN32_PATH ) then
    setenv LD_LIBRARYN32_PATH "${XERCESPLATFORM}"
  else
    setenv LD_LIBRARYN32_PATH "${XERCESPLATFORM}:${LD_LIBRARYN32_PATH}"   
  endif
  if ( ! $?LD_LIBRARY64_PATH ) then
    setenv LD_LIBRARY64_PATH "${XERCESPLATFORM}"
  else
    setenv LD_LIBRARY64_PATH "${XERCESPLATFORM}:${LD_LIBRARY64_PATH}"
  endif
else
  if ( ! $?LD_LIBRARY_PATH ) then
    setenv LD_LIBRARY_PATH "${XERCESPLATFORM}"
  else
    setenv LD_LIBRARY_PATH "${XERCESPLATFORM}:${LD_LIBRARY_PATH}"
  endif
endif

#############################################################################

setenv PATH "${HPCTOOLKIT}/bin/${HPCTOOLKIT_PLATFORM}:${HPCTOOLKIT}/bin:${PATH}"
